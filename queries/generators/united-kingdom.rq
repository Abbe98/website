SELECT DISTINCT
  ?qid
  ?orgLabel
  ?orgDescription
  ?type
  ?typeLabel
  (SAMPLE(?website) AS ?officialWebsite) # note that SAMPLE in Blazegraph always picks the first object
  (SAMPLE(?email) AS ?officialEmail)
  (SAMPLE(?citizensInitiatives ) AS ?citizensInitiativesURL)
  ?country
WHERE {
  BIND(wd:Q145 AS ?country)

  VALUES ?type {
    wd:Q2500378 # department of the United Kingdom Government (23)
  }

  ?org wdt:P31 ?type .

  ?org wdt:P17 ?country .
  OPTIONAL { ?org wdt:P856 ?website }
  OPTIONAL { ?org wdt:P968 ?email }
  OPTIONAL { ?org wdt:P9732 ?citizensInitiatives }

  MINUS { ?org wdt:P576 [] }

  BIND(REPLACE(STR(?org), "http://www.wikidata.org/entity/", "") AS ?qid)

  SERVICE wikibase:label { bd:serviceParam wikibase:language "en,[AUTO_LANGUAGE]". }
}
GROUP BY ?qid ?orgLabel ?orgDescription ?type ?typeLabel ?country
ORDER BY ?qid
